buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.50"
        classpath "gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.22.1"
        classpath "gradle.plugin.org.kravemir.gradle.sass:GradleSassPlugin:1.2.2"
        classpath "org.postgresql:postgresql:42.2.8"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.50"
    id "org.asciidoctor.convert" version "2.3.0"
    id "org.springframework.boot" version "2.2.0.M3"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "com.palantir.docker" version "0.22.1"
    id "io.gitlab.arturbosch.detekt" version "1.0.1"
    id "jacoco"
    id "org.kravemir.gradle.sass" version "1.2.4"
    id "org.flywaydb.flyway" version "6.0.4"
    id "name.remal.check-updates" version "1.0.156"
}

group = "io.saas"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = JavaVersion.VERSION_11

project.ext.set("snippetsDir", "build/snippets")

repositories {
    mavenCentral()
    jcenter()
    maven { url "http://repo1.maven.org/maven2/" }
    maven { url = uri "https://repo.spring.io/milestone" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

flyway {
    url = 'jdbc:postgresql://localhost:5533/saas-starter'
    user = 'postgres'
}

dependencies {
    // dependencies
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-mail"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.10.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.springframework.session:spring-session-core"
    implementation "org.passay:passay:1.5.0"
    implementation "commons-beanutils:commons-beanutils:1.9.4"
    compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity5:3.0.4.RELEASE"
    runtime 'org.postgresql:postgresql:42.2.6'

    // webjars
    compile "org.webjars:bootstrap:4.3.1"
    compile "org.webjars:jquery:3.4.1"
    compile "org.webjars:popper.js:1.15.0"

    // Spock
    testCompile "org.codehaus.groovy:groovy-all:2.5.8"
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"
    testCompile "org.hamcrest:hamcrest-core:2.1" // only necessary if Hamcrest matchers are used

    testImplementation ("org.springframework.security:spring-security-test") {
        exclude module: "mockito-core"
    }
    testImplementation "org.springframework.restdocs:spring-restdocs-mockmvc"
    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude(module : "junit")
        exclude(module : "mockito-core")
    }
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "com.ninja-squad:springmockk:1.1.3"
    testImplementation "com.h2database:h2:1.4.199"
    // DEV only
    compile "org.springframework.boot:spring-boot-devtools"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.0.1"
}

tasks.withType(Test) {
    useJUnitPlatform()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = sourceCompatibility
    }
}

task unpack(type: Copy) {
    dependsOn bootJar
    from(zipTree(tasks.bootJar.outputs.files.singleFile))
    into("$buildDir/dependency")
}

docker {
    name "${project.name}"
    dockerfile file('Dockerfile')
    copySpec.from(tasks.unpack.outputs).into("dependency")
    buildArgs(['DEPENDENCY': "dependency"])
}

bootRun {
    sourceResources sourceSets.main
}

springBoot {
    buildInfo()
}

asciidoctor {
    inputs.dir(project.snippetsDir)
    dependsOn(test)
}

detekt {
    version = detektVersion
    input = files("${projectDir}/src/main/kotlin")
    config = files("${projectDir}/detekt.yml") // Code style rules file.
    filters = files(".*/res/.*,.*build/.*")
    reports {
        html {
            enabled = true
            destination = file("$buildDir/reports/detekt/detekt.html")
        }
    }
}

sass {
    main {
        srcDir = file("$projectDir/src/main/scss")
        outDir = file("$projectDir/src/main/resources/public")
        outSubDir = "css"
        registerInSourceSets('main')
        minify = true
    }
}

configure(sass) {
    group = BasePlugin.BUILD_GROUP
    description = "Compile sass and copy output to valid target directory"
}
